repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { url 'https://dl.bintray.com/kotlin/exposed' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-releases' }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'net.corda.plugins.cordapp'
apply plugin: 'net.corda.plugins.publish-utils'
apply plugin: 'net.corda.plugins.cordformation'
apply plugin: 'net.corda.plugins.quasar-utils'
apply plugin: 'maven-publish'

cordapp {
    signing {
        enabled false
    }
    targetPlatformVersion 4
    minimumPlatformVersion 3
    contract {
        name "Corda Training Material"
        vendor "R3"
        licence "Contact R3 for Java Source Contract License."
        versionId 1
    }
    workflow {
        name "Corda Training Material"
        vendor "R3"
        licence "Contact R3 for Java Source Workflow License."
        versionId 1
    }
}

sourceSets {
    main {
        resources {
            srcDir "../config/dev"
        }
    }
    test {
        resources {
            srcDir "../config/test"
        }
    }
}

dependencies {
    testCompile "junit:junit:$junit_version"

    // Corda integration dependencies
    cordaCompile "$corda_release_distribution:corda-core:$corda_release_version"
    cordaCompile "$corda_release_distribution:corda-finance-contracts:$corda_release_version"
    cordaCompile "$corda_release_distribution:corda-finance-workflows:$corda_release_version"
    cordaCompile "$corda_release_distribution:corda-jackson:$corda_release_version"
    cordaCompile "$corda_release_distribution:corda-rpc:$corda_release_version"
    cordaCompile "$corda_release_distribution:corda-node-api:$corda_release_version"
    cordaCompile "$corda_release_distribution:corda-webserver-impl:$corda_release_version"
    cordaRuntime "$corda_release_distribution:corda:$corda_release_version"
    cordaRuntime "$corda_release_distribution:corda-webserver:$corda_release_version"

    testCompile "$corda_release_distribution:corda-test-utils:$corda_release_version"
    testCompile "$corda_release_distribution:corda-node-driver:$corda_release_version"


    // GraphStream: For visualisation (required by TemplateClientRPC app)
    compile "org.graphstream:gs-core:1.3"
    compile("org.graphstream:gs-ui:1.3") {
        exclude group: "bouncycastle"
    }

    // CorDapp dependencies
    // Specify your cordapp's dependencies below, including dependent CorDapps
    cordapp "$corda_release_distribution:corda-finance-contracts:$corda_release_version"
    cordapp "$corda_release_distribution:corda-finance-workflows:$corda_release_version"
    cordapp "$corda_release_distribution:corda-confidential-identities:$corda_release_version"

}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-parameters" // Required for shell commands.
}

task deployNodes(type: net.corda.plugins.Cordform, dependsOn: ['jar']) {

    delete "./build/nodes"
    directory "./build/nodes"

    signing {
        enabled false
    }

    nodeDefaults {
        cordapp("$corda_release_distribution:corda-finance-contracts:$corda_release_version")
        cordapp("$corda_release_distribution:corda-finance-workflows:$corda_release_version")
        cordapp("$corda_release_distribution:corda-confidential-identities:$corda_release_version")
        rpcUsers = [[ user: "user1", "password": "password", "permissions": ["ALL"]]]
    }

    node {
        name "O=Notary,L=London,C=GB"
        notary = [validating: false]
        p2pPort 10002
        rpcSettings {
            useSsl false
            standAloneBroker false
            address "0.0.0.0:10003"
            adminAddress "0.0.0.0:10103"
        }
    }
    node {
        name "O=ParticipantA,L=London,C=GB"
        p2pPort 10007
        webPort 10009
        rpcSettings {
            useSsl false
            standAloneBroker false
            address "0.0.0.0:10008"
            adminAddress "0.0.0.0:10108"
        }
    }
    node {
        name "O=ParticipantB,L=New York,C=US"
        p2pPort 10010
        webPort 10012
        rpcSettings {
            useSsl false
            standAloneBroker false
            address "0.0.0.0:10011"
            adminAddress "0.0.0.0:10111"
        }
    }
    node {
        name "O=ParticipantC,L=Paris,C=FR"
        p2pPort 10013
        webPort 10015
        rpcSettings {
            useSsl false
            standAloneBroker false
            address "0.0.0.0:10014"
            adminAddress "0.0.0.0:10114"
        }
    }
}
